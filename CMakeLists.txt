cmake_minimum_required(VERSION 3.1.0)
project(uiuc-shib)
set(CMAKE_CXX_STANDARD 11)

include(ExternalProject)

ExternalProject_Add(AWS_SDK_CPP
    PREFIX vendor
    INSTALL_DIR vendor

    GIT_REPOSITORY https://github.com/aws/aws-sdk-cpp.git
    GIT_TAG 1.7.327

    CMAKE_ARGS -DCMAKE_INSTALL_PREFIX=${CMAKE_CURRENT_BINARY_DIR}/vendor
    CMAKE_CACHE_ARGS
        -DENABLE_UNITY_BUILD:BOOL=ON
        -DBUILD_SHARED_LIBS:BOOL=OFF
        -DSIMPLE_INSTALL:BOOL=ON
        -DBUILD_ONLY:STRING=dynamodb;secretsmanager
        -DENABLE_TESTING:BOOL=OFF
        -DAUTORUN_UNIT_TESTS:BOOL=OFF
    BUILD_IN_SOURCE 0

    TEST_COMMAND ""
)
set(AWS_SDK_CPP_PREFIX_PATH
    ${CMAKE_CURRENT_BINARY_DIR}/vendor/
    ${CMAKE_CURRENT_BINARY_DIR}/vendor/src/AWS_SDK_CPP-build/.deps/install/
)

set(UIUC_SHIBPLUGINS_INSTALL_DIR ${CMAKE_CURRENT_BINARY_DIR} CACHE PATH "Place to install project binaries to")
ExternalProject_Add(UIUC_SHIBPLUGINS
    PREFIX      ${CMAKE_CURRENT_BINARY_DIR}
    TMP_DIR     ${CMAKE_CURRENT_BINARY_DIR}/project-tmp
    STAMP_DIR   ${CMAKE_CURRENT_BINARY_DIR}/project-stamp
    SOURCE_DIR  ${CMAKE_CURRENT_SOURCE_DIR}/project
    BINARY_DIR  ${CMAKE_CURRENT_BINARY_DIR}/project-build
    INSTALL_DIR ${UIUC_SHIBPLUGINS_INSTALL_DIR}

    CMAKE_ARGS -DCMAKE_INSTALL_PREFIX=${UIUC_SHIBPLUGINS_INSTALL_DIR}
    CMAKE_CACHE_ARGS
        -DAWS_SDK_CPP_PREFIX_PATH:STRING=${AWS_SDK_CPP_PREFIX_PATH}
    BUILD_IN_SOURCE 0

    DEPENDS AWS_SDK_CPP
)
